plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools
    resourcePrefix libConfig.resourcePrefix

    sourceSets {
        test.java.srcDirs += ["${project(':core').projectDir}/src/test/java"]
    }
    defaultConfig {

        gradle.startParameter.taskNames.each {
            if (it.contains("AndroidTest")) {
                //io.mockk has a bug that does not allow use a version of minSdk less than 26
                minSdkVersion 26
            } else {
                minSdkVersion buildConfig.minSdk
            }
        }

        targetSdkVersion buildConfig.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        def githubRef = System.getenv("GITHUB_REF")
        if (githubRef != null) {
            buildConfigField("String", "SDK_VERSION", "\"${githubRef.split('/')?.getAt(2)}\"")
        } else {
            buildConfigField("String", "SDK_VERSION", "\"dev\"")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    'proguard-rules-test.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
            testLogging.showStandardStreams = true
            testLogging {
                events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
            }
        }
    }
}

dependencies {
    implementation timber
    implementation lifecycle.libs
    implementation networking.libs
    implementation rxJava

    api project(":core")
    api project(":session")
    implementation kotlinCore
    implementation support
    implementation activity
    implementation viewmodel
    implementation navigation
    implementation koin
    //debugImplementation leakCanary

    testImplementation mockkTest
    androidTestImplementation espresso
}

apply from: '../buildsystem/publish.gradle'