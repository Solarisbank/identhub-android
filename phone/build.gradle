plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools
    resourcePrefix libConfig.resourcePrefix

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            def githubRef = System.getenv("GITHUB_REF")
            if (githubRef != null) {
                maven(MavenPublication) {
                    from components.release
                    groupId = project.group
                    artifactId = 'identhub-android-phone'
                    // Version should match the release tag
                    version = System.getenv("GITHUB_REF")?.split('/')?.getAt(2)


                    pom {
                        name = 'IdentHub'
                        description = 'Solarisbank IdentHub Android SDK'
                        developers {
                            developer {
                                name = 'Solarisbank AG'
                            }
                        }

                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

dependencies {
    implementation project(":core")
    implementation project(":session")
    implementation kotlinCore
    implementation support
    implementation activity
    implementation koin
    implementation networking.libs
    implementation rxJava
    implementation navigation
    implementation timber
    testImplementation mockkTest
    testImplementation koinTest
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}